  extern probe ProbeAndCopy
  extern probe (INIT) ProbeInit
  extern PURE UINT64 UlongToPtr(UINT32 ptr)
  extern probe (READ UINT32) ProbeAndReadU32
  extern probe (WRITE UINT64) WriteU64

  aligned
  typedef struct _T(UINT32 bound) {
      UINT32 t1;
      UINT32 t2 { t2 <= bound };    
  } T;

  aligned
  typedef struct _S64(UINT32 bound, EVERPARSE_COPY_BUFFER_T dest) {
      UINT32 s1 { s1 <= bound };
      T(s1) *ptrT probe ProbeAndCopy(length=sizeof(T), destination=dest);
      UINT32 s2;
  } S64;

  aligned
  typedef struct _R64(EVERPARSE_COPY_BUFFER_T destS, EVERPARSE_COPY_BUFFER_T destT) {
      UINT32 r1;
      S64(r1, destT) *ptrS probe ProbeAndCopy(length=sizeof(S64), destination=destS);
  } R64;

  specialize (pointer(*), pointer(UINT32)) R64 R32;

  entrypoint
  typedef struct R(Bool requestor32, EVERPARSE_COPY_BUFFER_T destS, EVERPARSE_COPY_BUFFER_T destT) {
      switch (requestor32) {
          case true: R32(destS, destT) r32;
          default: R64(destS, destT) r64;
      } field;
  } R;